test: (output expected) {
    output.is(expected).then('PASS' 'FAIL').print()
}

'Testing Simple Tables, Methods, and Assignment'.print()
chain1: (x) { x.+(2).is(3) }
chain2: (x) { x.+(2).*(3).sqrt() }
test(chain1(1) true)
test(chain2(1) 3)

'\nTesting Multi-Argument Table Execution and Table Variable Storage'.print()
table: (x y) {
    z: x.+(y)
    z.*(10)
}
test(table(5 5) 100)

'\nTesting Class Functionality'.print()
Point: (x y) {
    add: (p) { Point(x.+(p.x) y.+(p.y)) }
    distanceTo: (p) { 
        x.-(p.x).^(2).+(y.-(p.y).^(2)).sqrt()
    }
}
p1: Point(0 0)
p2: p1.add(Point(1 1))
test(p1.distanceTo(p2) 2.sqrt())

'\nRecursive Fibonacci Test'.print()
fib: (n a b) {
    nextResult: n.>(0).then((){ fib(n.-(1) a.+(b) a) } (){ a })
    nextResult()
}
test(fib( 10 1 0 ) 89)

'\nTesting past closure bugs'.print()
x: 5
y: { x: 4 }
test(y.x 4)

j: (i) { i.*(2) }
test(2.*(j(5)) 20)

'\nTesting array operations'.print()
z: { 98.2 85.2 90.2 75 gradesOf: 'Bob' }
test(z.sum()./( z.length() ) 87.15)

neato: { 1 2 3 4 5 }
squaredNeato: neato.map( (i){ i.^(2) } )
test( squaredNeato.get(4) 25 )